apply plugin: 'com.android.application'

android {
    compileSdkVersion 33
    def _patch
    def versionPropsFile = file("version.properties")
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        _patch = versionProps['PATCH'].toInteger() + 1
        versionProps['PATCH'] = _patch.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new Exception("Could not read version.properties!")
        //_patch = "E"
    }
    def _versionName = "0." + _patch
    defaultConfig {
        applicationId "ua.com.programmer.simpleremote"
        minSdkVersion 21
        targetSdkVersion 33
        versionName _versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "main"
    productFlavors {
        full {
            dimension "main"
            versionCode _patch
            versionName _versionName + '.f'
        }
        standart {
            dimension "main"
            versionCode _patch
            versionName _versionName + '.s'
        }
    }
    buildToolsVersion '31.0.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //volley
    implementation 'com.android.volley:volley:1.2.1'
    //glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // CameraX dependencies
    def camerax_version = "1.2.0-beta02"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"

    implementation 'com.google.mlkit:barcode-scanning:17.0.2'

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:28.4.2')
    //implementation 'com.google.firebase:firebase-core:20.0.0'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'