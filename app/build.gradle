apply plugin: 'com.android.application'

android {
    namespace 'ua.com.programmer.simpleremote'
    compileSdk 34
    def _patch
    def versionPropsFile = file("version.properties")
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        _patch = versionProps['PATCH'].toInteger() + 1
        versionProps['PATCH'] = _patch.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new Exception("Could not read version.properties!")
        //_patch = "E"
    }
    def _versionName = "0." + _patch
    defaultConfig {
        applicationId "ua.com.programmer.simpleremote"
        minSdk 21
        targetSdk 34
        versionName _versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions = ["version"]
    productFlavors {
        full {
            dimension "version"
            versionCode _patch
            versionName _versionName + '.f'
        }
        standart {
            dimension "version"
            versionCode _patch
            versionName _versionName + '.s'
        }
    }
//    buildToolsVersion '31.0.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    //volley
    implementation 'com.android.volley:volley:1.2.1'
    //glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    // CameraX dependencies
    def camerax_version = "1.2.3"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"

    implementation 'com.google.mlkit:barcode-scanning:17.2.0'

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:28.4.2')
    //implementation 'com.google.firebase:firebase-core:20.0.0'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'